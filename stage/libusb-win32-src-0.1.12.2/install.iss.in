; LIBUSB-WIN32, Generic Windows USB Library
; Copyright (c) 2002-2005 Stephan Meyer <ste_meyer@web.de>
;
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
; 
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
; 
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA


[Setup]
AppName = LibUSB-Win32
AppVerName = LibUSB-Win32-@VERSION@
AppId = LibUSB-Win32
AppPublisher = LibUSB-Win32 
AppPublisherURL = http://libusb-win32.sourceforge.net
AppVersion = @VERSION@
DefaultDirName = {pf}\LibUSB-Win32
DefaultGroupName = LibUSB-Win32
LicenseFile = installer_license.txt
Compression = lzma
SolidCompression = yes
OutputDir = .
OutputBaseFilename = libusb-win32-filter-bin-@VERSION@
; requires Win98SE, Win2k, or higher
MinVersion = 4.1.2222, 5 
PrivilegesRequired=admin


[Files]
; XP 32bit or lower
Source: "libusb0.sys"; DestDir: "{win}\system32\drivers"; Flags: uninsneveruninstall replacesameversion restartreplace promptifolder; OnlyBelowVersion: 0, 5.02.3790
Source: "libusb0.dll"; DestDir: "{win}\system32"; Flags: uninsneveruninstall replacesameversion restartreplace promptifolder; OnlyBelowVersion: 0, 5.02.3790

; XP 64bit or higher
Source: ".\ddk_make\libusb0.sys"; DestDir: "{win}\system32\drivers"; Flags: uninsneveruninstall replacesameversion restartreplace promptifolder; MinVersion: 0, 5.02.3790
Source: ".\ddk_make\libusb0.dll"; DestDir: "{win}\system32"; Flags: uninsneveruninstall replacesameversion restartreplace promptifolder; MinVersion: 0, 5.02.3790
Source: "libusb0.dll"; DestDir: "{syswow64}"; Flags: uninsneveruninstall replacesameversion restartreplace promptifolder; MinVersion: 0, 5.02.3790

Source: "@BIN_DIST_DIR@\lib\*.*"; DestDir: "{app}\lib"; Flags: recursesubdirs
Source: "@BIN_DIST_DIR@\include\*.*"; DestDir: "{app}\include"; Flags: recursesubdirs
Source: "@BIN_DIST_DIR@\examples\*.*"; DestDir: "{app}\examples"; Flags: recursesubdirs

Source: "install-filter.exe"; DestDir: "{app}\bin";
Source: "testlibusb.exe"; DestDir: "{app}\bin";
Source: "testlibusb-win.exe"; DestDir: "{app}\bin";

Source: "@BIN_DIST_DIR@\*.*"; DestDir: "{app}"
Source: "@SRC_DIST_DIR@\*.*"; DestDir: "{app}\src"; Flags: recursesubdirs


[Icons] 
Name: "{group}\Test Program"; Filename: "{app}\bin\testlibusb-win.exe"; 
Name: "{group}\Uninstall LibUsb-Win32"; Filename: "{uninstallexe}"
Name: "{group}\GPL License"; Filename: "{app}\COPYING_GPL.txt"
Name: "{group}\LGPL License"; Filename: "{app}\COPYING_LGPL.txt"


[Run]
Filename: "{app}\bin\install-filter.exe"; Parameters: "-i"; StatusMsg: "Creating kernel service (this may take a few seconds) ..."
Filename: "{app}\bin\testlibusb-win.exe"; Description: "Run test application"; Flags: postinstall


[UninstallRun]
Filename: "{app}\bin\install-filter.exe"; Parameters: "-u"

[Messages]
StatusUninstalling=Uninstalling %1 (this may take a few seconds) ...


